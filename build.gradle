buildscript {
	repositories {
		gradlePluginPortal()
		jcenter()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.5-SNAPSHOT"
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version "1.4.0"
}

apply plugin: net.fabricmc.loom.LoomGradlePlugin

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def ENV = System.getenv()
def buildVersion = ENV.BUILD_NUMBER ? "${mod_verison}.${ENV.BUILD_NUMBER}" : "${mod_verison}-beta"
version = "${minecraft_version}-" + buildVersion + "-universal"

archivesBaseName = "${mod_name}-Fabirc"

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}


tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}


repositories {
	jcenter()
	maven { url = "https://maven.fabricmc.net/" }
	maven { url = 'http://maven.sargunv.s3-website-us-west-2.amazonaws.com/' }
	maven { url = "https://minecraft.curseforge.com/api/maven" }


}


dependencies {
	minecraft "com.mojang:minecraft:" + "${minecraft_version}"
	mappings "net.fabricmc:yarn:" + "${mappings_version}"
	modCompile "net.fabricmc:fabric-loader:" + "${loader_version}"

	modCompile 'me.shedaniel.cloth:config-2:' + "${cloth_config_version}"
	modCompile "io.github.prospector:modmenu:1.7.6+build.115"
	modCompile "auto-config:auto:config:" + "${auto_config_version}"

	include 'me.shedaniel.cloth:config-2:'+ "${cloth_config_version}"
	include "auto-config:auto:config:" + "${auto_config_version}"

}


curseforge {
	if (project.hasProperty('unreal_curse_api_key')){
		apiKey = project.getProperty('unreal_curse_api_key')
	}
	project {
		id = "${curseforge_id}"
		changelog = getGitChangelog
		changelogType = 'markdown'
		releaseType = 'release'
		addGameVersion project.hasProperty("curseforge_minecraft_version") ? "${curseforge_minecraft_version}" : "${minecraft_version}"
		addGameVersion "Java 8"
		addGameVersion "Fabric"
		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
		relations {
			embeddedLibrary 'cloth-config'
			embeddedLibrary 'auto-config'
			optionalDependency 'modmenu'
		}
		afterEvaluate {
			uploadTask.dependsOn(remapJar)
		}
	}
	options{
		forgeGradleIntegration = false
	}
}