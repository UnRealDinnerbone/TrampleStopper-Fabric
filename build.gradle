import net.fabricmc.loom.LoomGradlePlugin

buildscript {
	repositories {
		gradlePluginPortal()
		jcenter()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.5-SNAPSHOT"
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version "1.2.0"
}

apply plugin: LoomGradlePlugin
apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def ENV = System.getenv()
def buildVersion = ENV.BUILD_NUMBER ? "${mod_verison}.${ENV.BUILD_NUMBER}" : "${mod_verison}-beta"
version = buildVersion + "-universal"

archivesBaseName = "${mod_name}"

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}


tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}


repositories {
	maven { url = "https://maven.fabricmc.net/" }
	maven { url = "https://minecraft.curseforge.com/api/maven" }
}


dependencies {
	minecraft "com.mojang:minecraft:" + "${minecraft_version}"
	mappings "net.fabricmc:yarn:" + "${mappings_version}"
	modCompile "net.fabricmc:fabric-loader:" + "${loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:" + "${fabric_version}"

	include "cloth-config:ClothConfig2:0.2.0"
	include "auto-config:auto:config:1.1.2+mc1.14.1"

	modCompile "cloth-config:ClothConfig2:0.2.0"
	modCompile "auto-config:auto:config:1.1.2+mc1.14.1"
	modCompile "io.github.prospector.modmenu:ModMenu:1.6.2-92"

}


curseforge {
	if (project.hasProperty('unreal_curse_api_key')){
		apiKey = project.getProperty('unreal_curse_api_key')
	}
	project {
		id = "${curseforge_id}"
		changelog = getGitChangelog
		changelogType = 'markdown'
		releaseType = 'release'
		addGameVersion "${minecraft_version}"
		addGameVersion "Java 8"
		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
		relations {
			embeddedLibrary 'cloth-config'
			embeddedLibrary 'auto-config'
			requiredDependency 'fabric'
			optionalDependency 'modmenu'
		}
		afterEvaluate {
			uploadTask.dependsOn(remapJar)
		}
	}
	options{
		forgeGradleIntegration = false
	}
}